
model {

  # Priors to describe heterogeneity among species in community
  for(k in 1:M){                  # Loop over all species in augmented list
    lpsi[k] ~ dnorm(mu.lpsi, tau.lpsi)
    lp[k] ~ dnorm(mu.lp, tau.lp)
  }

  # Hyperpriors to describe full community
  omega ~ dunif(0,1)              # Data augmentation or 'occupancy' parameter
  mu.lpsi ~ dnorm(0,0.001)        # Community mean of occupancy (logit)
  mu.lp ~ dnorm(0,0.001)          # Community mean of detection (logit)
  tau.lpsi <- pow(sd.lpsi, -2)
  sd.lpsi ~ dunif(0,5)            # Species heterogeneity in logit(psi)
  tau.lp <- pow(sd.lp, -2)
  sd.lp ~ dunif(0,5)              # Species heterogeneity in logit(p)

  # Superpopulation process:this is the 'paramater expansion' part of PX-DA
  for(k in 1:M){
    w[k] ~ dbern(omega)           # Metacommunity membership indicator
  }                               # (or data augmentation variable)

  # Ecological model for latent occurrence z (process model)
  for(k in 1:M){
    mu.psi[k] <- w[k] * psi[k]    # species not part of community zeroed out for z
    logit(psi[k]) <- lpsi[k]
    for (i in 1:nsite) {
      z[i,k] ~ dbern(mu.psi[k])
    }
  }

  # Observation model for observed detection frequencies
  for(k in 1:M){
    logit(p[k]) <- lp[k]
    for (i in 1:nsite) {
      mu.p[i,k] <- z[i,k] * p[k]  # non-occurring species are zeroed out for p
      yaug[i,k] ~ dbin(mu.p[i,k], nrep[i])
    }
  }

  # Derived quantities
  for(k in 1:M){
     Nocc.fs[k] <- sum(z[,k])     # Number of occupied sites among the 267
  }
  for (i in 1:nsite) {
    Nsite[i] <- sum(z[i,])        # Number of occurring species at each site
  }
  n0 <- sum(w[(nspec+1):(nspec+nz)]) # Number of unseen species in metacommunity
  Ntotal <- sum(w[])              # Total metacommunity size (= nspec + n0)
}

